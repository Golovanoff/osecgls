#Использовать "../.."

&ЖурналПроверки
Перем Лог;

&Пластилин("ПроверкаЗапросаНаСлияние", Тип = "Массив")
Перем ПроверкиМР;

&Контроллер("/check")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ТочкаМаршрута("")
Процедура ПроверитьЗапросНаСлияние(ТелоЗапросОбъект, Ответ) Экспорт
	
	Лог.Отладка("Получен запрос на проверку: %1 %2", ТелоЗапросОбъект["id"], ТелоЗапросОбъект["title"]);
	
	РезультатПроверки = ПроверитьМР(ТелоЗапросОбъект);

	Результат = Новый Структура;
	Результат.Вставить("status", РезультатПроверки.Статус);
	Если ЗначениеЗаполнено(РезультатПроверки.Сообщение) Тогда
		Результат.Вставить("message", РезультатПроверки.Сообщение);	
	КонецЕсли;
	
	Ответ.УстановитьТипКонтента("json");
	Ответ.ТелоТекст = Общие.Сериализовать(Результат);
КонецПроцедуры

Функция ПроверитьМР(Знач ТелоЗапроса)

	Результат = Общие.РезультатПроверки(Истина);

	Лог.Информация("Включено проверок: %1", ПроверкиМР.Количество());

	Для Каждого ПроверкаМР Из ПроверкиМР Цикл
		РезультатПроверки = ПроверкаМР.ПроверитьМР(ТелоЗапроса);
		Лог.Отладка("Результат проверки: %1 %2", РезультатПроверки.Статус, РезультатПроверки.Сообщение);
		Результат = Общие.ОбъединитьРезультатыПроверок(Результат, РезультатПроверки);
	КонецЦикла;

	Возврат Результат;
КонецФункции
